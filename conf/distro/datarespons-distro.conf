DISTRO_VERSION = "scarthgap"
DISTRO_NAME = "Data Respons distro"

DISTRO_FEATURES = " \
	${@bb.utils.contains('MACHINE_FEATURES', 'screen', 'wayland opengl pulseaudio', '',d)} \
	${@bb.utils.contains('MACHINE_FEATURES', '3g', 'polkit', '',d)} \
	${@bb.utils.filter('MACHINE_FEATURES', 'pci wifi bluetooth ext2 usbhost alsa efi', d)} \
	argp largefile xattr gobject-introspection-data pam usrmerge \
	systemd polkit \
"

# Build fails when assembling rootfs for 
# images including external kernel modules.
# All recipes are verified to have valid sdpx data
# where the match is based on spdx name being
# equal to PN.
# External modules append kernel version to PN
# during build time and spdx creating but not 
# when verifying PN to spdx name.
#
# Disable for now.
INHERIT_DISTRO:remove = "create-spdx"

DISTRO_FEATURES_BACKFILL_CONSIDERED = "pulseaudio"
INIT_MANAGER = "systemd"

PACKAGECONFIG:remove:pn-systemd = "\
	binfmt \
	backlight \
	vconsole \
	quotacheck \
	hibernate \
	ima \
	smack \
	networkd \
"

# Make sure serial-getty@.service is installed.
PACKAGECONFIG:append:pn-systemd = " serial-getty-generator"

# Boot into our systemd target
SYSTEMD_DEFAULT_TARGET ?= "datarespons-apps.target"

# Build networkmanager with modemmanager support
PACKAGECONFIG:append:pn-networkmanager = " modemmanager"
# We rely on dns from dhcp
PACKAGECONFIG:remove:pn-networkmanager = "dnsmasq"
# Do not manage bluetooth in networkmanager
PACKAGECONFIG:remove:pn-networkmanager = "bluez5"
# Disable mbim dependency as we use qmi for modems
PACKAGECONFIG:remove:pn-modemmanager = "mbim"
PACKAGECONFIG:remove:pn-libqmi = "mbim"
# Disable unused vala bindings
PACKAGECONFIG:remove:pn-modemmanager = "vala"
PACKAGECONFIG:remove:pn-networkmanager = "vala"

# We don't want to route qt5 to x11 through wayland
PACKAGECONFIG:remove:pn-qtwayland = "xcomposite-egl xcomposite-glx"

# Build python3 bindings for libiio
PACKAGECONFIG:append:pn-libiio = " libiio-python3"

# Add python bindings generator for grpc-native recipes
PACKAGECONFIG:append:pn-grpc-native = " python"

# Replace busybox will full base-utils
PREFERRED_PROVIDER_virtual/base-utils = "packagegroup-datarespons-base-utils"
VIRTUAL-RUNTIME_base-utils = "packagegroup-datarespons-base-utils"
VIRTUAL-RUNTIME_base-utils-hwclock = "util-linux-hwclock"
VIRTUAL-RUNTIME_base-utils-syslog = ""

# obex-profiles pulls in libical which in turn pulls in 
# icu which adds ~30MB to image size.
# Investigate further if needed, disable OBEX for now.
PACKAGECONFIG:remove:pn-bluez5 = "obex-profiles"

# polkit has options for js-engine.
# The default "mozjs" is ~15MB and pulls in icu which adds ~30MB to image size.
# Switch to alternative "duktape".
PACKAGECONFIG:remove:pn-polkit = "mozjs"
PACKAGECONFIG:append:pn-polkit = " duktape"
