DISTRO_VERSION = "kirkstone"
DISTRO_NAME = "Data Respons distro"

DISTRO_FEATURES = " \
	${@bb.utils.contains('MACHINE_FEATURES', 'screen', 'wayland opengl x11 pulseaudio', '',d)} \
	${@bb.utils.contains('MACHINE_FEATURES', '3g', 'polkit', '',d)} \
	${@bb.utils.filter('MACHINE_FEATURES', 'pci wifi bluetooth ext2 usbhost alsa efi', d)} \
	argp largefile xattr gobject-introspection-data pam \
"

DISTRO_FEATURES_BACKFILL_CONSIDERED = "pulseaudio"
INIT_MANAGER = "systemd"

PACKAGECONFIG:remove:pn-systemd = "\
	binfmt \
	backlight \
	vconsole \
	quotacheck \
	hibernate \
	ima \
	smack \
	networkd \
"

# NetworkManager will be used instead of ofono
RDEPENDS:packagegroup-base-3g:remove = "ofono"

# Boot into our systemd target
SYSTEMD_DEFAULT_TARGET ?= "datarespons-apps.target"

PACKAGECONFIG:remove:pn-networkmanager = "bluez dnsmasq"
PACKAGECONFIG:remove:pn-modemmanager = "mbim"
PACKAGECONFIG:pn-wpa-supplicant = "openssl"

# We don't want to route qt5 to x11 through wayland
PACKAGECONFIG:remove:pn-qtwayland = "xcomposite-egl xcomposite-glx"

# Build python3 bindings for libiio
PACKAGECONFIG:append:pn-libiio = " libiio-python3"

# rngd has bug preventing systemd shutdown, we disable it by default
SYSTEMD_AUTO_ENABLE:pn-rng-tools = "disable"

# Keep vim-tiny small
PACKAGECONFIG:remove:pn-vim-tiny = "x11 gtkgui"

# Add python bindings generator for grpc-native recipes
PACKAGECONFIG:append:pn-grpc-native = " python"

# Replace busybox
VIRTUAL-RUNTIME_base-utils = "packagegroup-core-base-utils"
PREFERRED_PROVIDER_virtual/base-utils = "packagegroup-core-base-utils"
VIRTUAL-RUNTIME_vim = "vim-tiny"
VIRTUAL-RUNTIME_base-utils-hwclock = "util-linux-hwclock"
VIRTUAL-RUNTIME_base-utils-syslog = ""

# Enable debug gui for libinput
PACKAGECONFIG:pn-libinput = "gui"
